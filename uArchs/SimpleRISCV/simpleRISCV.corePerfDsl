/*********************************** Microaction Section ***********************************/
Connector {Pc, Pc_p, Pc_np}
Connector {Xa, Xb, Xd}

Resource {PC_Gen, Decoder, ALU, Multi, Reg, DPort_W(dMemModel), DPort_R(dMemModel), IPort_R(iMemModel)}
virtual Resource v_IPort

// General Microactions
Microaction {
    uA_IF (Pc -> v_IPort),
    uA_PC_GEN (Pc -> PC_Gen -> Pc_p),
    uA_ID (Decoder),
    uA_OF_A (Xa),
    uA_OF_B (Xb),
    uA_ALU_branch (ALU -> Pc_np),
    uA_ALU_mem (ALU),
    uA_MEM_W (DPort_W)
}

// Non-forwarding Microactions
Microaction {
    uA_ALU_arith_nfw (ALU),
    uA_MUL_nfw (Multi),
    uA_MEM_R_nfw (DPort_R),
    uA_WB_nfw (Reg -> Xd)
}

// Forwarding Microactions
Microaction {
    uA_ALU_arith_fw (ALU -> Xd),
    uA_MUL_fw (Multi -> Xd),
    uA_MEM_R_fw (DPort_R -> Xd),
    uA_WB_fw (Reg)
}

// Virtual Microactions
virtual Microaction {
    vuA_ALU_arith,
    vuA_MUL,
    vuA_MEM_R,
    vuA_WB
}

/*********************************** Stages & Pipeline ***********************************/
Stage {
    IF_stage (uA_IF, uA_PC_GEN),
    ID_stage (uA_ID, uA_OF_A, uA_OF_B),
    EX_stage (vuA_ALU_arith, vuA_MUL, uA_ALU_branch, uA_ALU_mem),
    MEM_stage (vuA_MEM_R, uA_MEM_W),
    WB_stage (vuA_WB)
}
Pipeline SimplePipeline (IF_stage -> ID_stage -> EX_stage -> MEM_stage -> WB_stage)

/*********************************** External Models ***********************************/
TraceValue {rs1, rs2, rd, pc, brTarget}

ConnectorModel regModel (
    link : StandardRegisterModel
    trace : {rs1, rs2, rd}
    connectorIn : Xd
    connectorOut : {Xa, Xb}
)

ConnectorModel dynBranchPredModel (
    link : DynamicBranchPredictModel
    trace : {pc, brTarget}
    connectorIn : {Pc_p, Pc_np}
    connectorOut : Pc
)

ConnectorModel staBranchPredModel (
    link : StaticBranchPredictModel
    trace : {pc, brTarget}
    connectorIn : {Pc_p, Pc_np}
    connectorOut : Pc
)

ConnectorModel noBranchPredModel (
    link : NoBranchPredictModel
    trace : {pc, brTarget}
    connectorIn : {Pc_p, Pc_np}
    connectorOut : Pc
)

ResourceModel iMemModel (
    link : DummyMemoryModel
)

ResourceModel dMemModel (
    link : DummyMemoryModel
)

/*********************************** Instruction Section ***********************************/
InstrGroup {
    Arith_Ra_Rb (add, sub, sll, xor, or, and, cadd),
    Mul (mul, rem),
    Arith_Ra (addi, sltiu, xori, ori, andi, caddi, cslli, caddi16sp),
    Arith_Rb (cmv),
    Arith_X (cli),
    Store (sb, sh, sw, csw),
    Load (lh, lw, lbu, lhu, clw),
    Branch_Ra_Rb (beq, bne, blt, bge, bltu, bgeu),
    Branch_Ra (cbeqz, cbnez),
    Default ([?])
}

InstrGroup {
    Arith_R_Type (add, sub, sll, xor, or, and, mul, rem),
    Branch_B_Type (beq, bne, blt, bge, bltu, bgeu),
    Load_I_Type (lb, lh, lw, lbu, lhu),
    Arith_I_Type (addi, slti, sltiu, xori, ori, andi),
    Store_S_Type (sb, sh, sw),
    Branch_CB_Type (cbeqz, cbnez),
    Arith_CR_Type (cadd),
    Arith_CI_Type (caddi, cslli),
    Load_CL_Type (clw),
    Store_CS_Type (csw),
    Move_CR_Type (cmv),
    Load_CI_Type (cli),
    Addi16sp_CI_Type (caddi16sp)
}

MicroactionMapping {
    [ALL] : {uA_IF, uA_PC_GEN, uA_ID},
    Arith_Ra_Rb : {uA_OF_A, uA_OF_B, vuA_ALU_arith, vuA_WB},
    Arith_Ra : {uA_OF_A, vuA_ALU_arith, vuA_WB},
    Arith_Rb : {uA_OF_B, vuA_ALU_arith, vuA_WB},
    Arith_X :  {vuA_ALU_arith, vuA_WB},
    Mul : {uA_OF_A, uA_OF_B, vuA_MUL, vuA_WB},
    Store : {uA_OF_A, uA_OF_B, uA_ALU_mem, uA_MEM_W},
    Load : {uA_OF_A, uA_ALU_mem, vuA_MEM_R, vuA_WB},
    Branch_Ra_Rb : {uA_OF_A, uA_OF_B, uA_ALU_branch},
    Branch_Ra : {uA_OF_A, uA_ALU_branch}
}

TraceValueMapping {
    [ALL] : {
        pc = "${ic.current_address_}"
    },
    Arith_R_Type : {
        rs1 = "${${BITFIELD rs1 [(0:19,15)]}}",
        rs2 = "${${BITFIELD rs2 [(0:24,20)]}}",
        rd  = "${${BITFIELD rd [(0:11,7)]}}"
    },
    Branch_B_Type : {
        rs1 = "${${BITFIELD rs1 [(0:19,15)]}}",
	rs2 = "${${BITFIELD rs2 [(0:24,20)]}}",
	brTarget = "${std::to_string(ic.current_address_ + (((etiss_int16)(${BITFIELD imm [(12:31,31)|(11:7,7)|(5:30,25)|(1:11,8)]} << 3)) >> 3))}"
    },
    Load_I_Type : {
        rs1 = "${${BITFIELD rs1 [(0:19,15)]}}",
	rd  = "${${BITFIELD rd [(0:11,7)]}}"
    },
    Arith_I_Type : {
        rs1 = "${${BITFIELD rs1 [(0:19,15)]}}",
	rd  = "${${BITFIELD rd [(0:11,7)]}}"
    },
    Store_S_Type : {
        rs1 = "${${BITFIELD rs1 [(0:19,15)]}}",
	rs2 = "${${BITFIELD rs2 [(0:24,20)]}}"
   },
   Branch_CB_Type : {
        rs1 = "${${BITFIELD rs1 [(0:9,7)]}}",
	brTarget = "${std::to_string(ic.current_address_ + (((etiss_int16)(${BITFIELD imm [(8:12,12)|(6:6,5)|(5:2,2)|(3:11,10)|(1:4,3)]} << 7)) >> 7))}"
   },
   Arith_CR_Type : {
        rs1 = "${${BITFIELD rs1 [(0:11,7)]}}",
	rs2 = "${${BITFIELD rs2 [(0:6,2)]}}",
	rd = "${${BITFIELD rd [(0:11,7)]}}"
   },
   Arith_CI_Type : {
        rs1 = "${${BITFIELD rs1 [(0:11,7)]}}",
	rd = "${${BITFIELD rd [(0:11,7)]}}"
   },
   Load_CL_Type	: {
        rs1 = "${${BITFIELD rs1 [(0:9,7)]}}",
	rd = "${${BITFIELD rd [(0:4,2)]}}"
   },
   Store_CS_Type : {
        rs1 = "${${BITFIELD rs1 [(0:9,7)]}}",
	rs2 = "${${BITFIELD rs2 [(0:4,2)]}}"
   },
   Move_CR_Type : {
        rs2 = "${${BITFIELD rs2 [(0:6,2)]}}",
	rd = "${${BITFIELD rd [(0:11,7)]}}"
   },
   Load_CI_Type : {
	rd = "${${BITFIELD rd [(0:11,7)]}}"
   },
   Addi16sp_CI_Type : {
	rs1 = "2",
	rd = "2"
   }
}

/*********************************** Variants ***********************************/

CorePerfModel SimpleRISCV_H_fw_DynBrPred (
    use Pipeline : SimplePipeline
    use ConnectorModel : {dynBranchPredModel, regModel}
    assign Resource : v_IPort = IPort_R
    assign Microaction : {
        vuA_ALU_arith = uA_ALU_arith_fw,
        vuA_MUL = uA_MUL_fw,
        vuA_MEM_R = uA_MEM_R_fw,
        vuA_WB = uA_WB_fw
    }
)